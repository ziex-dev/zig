const ConfigHeader = @This();

const std = @import("std");
const mem = std.mem;
const testing = std.testing;
const Build = std.Build;
const Io = std.Io;
const Step = Build.Step;
const Allocator = mem.Allocator;
const Writer = std.Io.Writer;

pub const Style = union(enum) {
    /// A configure format supported by autotools that uses `#undef foo` to
    /// mark lines that can be substituted with different values.
    autoconf_undef: Build.LazyPath,
    /// A configure format supported by autotools that uses `@FOO@` output
    /// variables.
    autoconf_at: Build.LazyPath,
    /// The configure format supported by CMake. It uses `@FOO@`, `${}` and
    /// `#cmakedefine` for template substitution.
    cmake: Build.LazyPath,
    /// The configure format supported by Meson. It uses `@FOO@`, and
    /// `#mesondefine` for template substitution.
    meson: Build.LazyPath,
    /// Instead of starting with an input file, start with nothing.
    blank,
    /// Start with nothing, like blank, and output a nasm .asm file.
    nasm,

    pub fn getPath(style: Style) ?Build.LazyPath {
        switch (style) {
            .autoconf_undef, .autoconf_at, .cmake, .meson => |s| return s,
            .blank, .nasm => return null,
        }
    }
};

pub const Value = union(enum) {
    undef,
    defined,
    boolean: bool,
    int: i64,
    ident: []const u8,
    string: []const u8,
};

step: Step,
values: std.StringArrayHashMap(Value),
/// This directory contains the generated file under the name `include_path`.
generated_dir: Build.GeneratedFile,
style: Style,
max_bytes: usize,
include_path: []const u8,
include_guard_override: ?[]const u8,

pub const base_id: Step.Id = .config_header;

pub const Options = struct {
    style: Style = .blank,
    max_bytes: usize = 2 * 1024 * 1024,
    include_path: ?[]const u8 = null,
    first_ret_addr: ?usize = null,
    include_guard_override: ?[]const u8 = null,
};

pub fn create(owner: *Build, options: Options) *ConfigHeader {
    const config_header = owner.allocator.create(ConfigHeader) catch @panic("OOM");

    var include_path: []const u8 = "config.h";

    if (options.style.getPath()) |s| default_include_path: {
        const sub_path = switch (s) {
            .src_path => |sp| sp.sub_path,
            .generated => break :default_include_path,
            .cwd_relative => |sub_path| sub_path,
            .dependency => |dependency| dependency.sub_path,
        };
        const basename = std.fs.path.basename(sub_path);
        if (mem.endsWith(u8, basename, ".h.in")) {
            include_path = basename[0 .. basename.len - 3];
        }
    }

    if (options.include_path) |p| {
        include_path = p;
    }

    const name = if (options.style.getPath()) |s|
        owner.fmt("configure {s} header {s} to {s}", .{
            @tagName(options.style), s.getDisplayName(), include_path,
        })
    else
        owner.fmt("configure {s} header to {s}", .{ @tagName(options.style), include_path });

    config_header.* = .{
        .step = .init(.{
            .id = base_id,
            .name = name,
            .owner = owner,
            .makeFn = make,
            .first_ret_addr = options.first_ret_addr orelse @returnAddress(),
        }),
        .style = options.style,
        .values = .init(owner.allocator),

        .max_bytes = options.max_bytes,
        .include_path = include_path,
        .include_guard_override = options.include_guard_override,
        .generated_dir = .{ .step = &config_header.step },
    };

    if (options.style.getPath()) |s| {
        s.addStepDependencies(&config_header.step);
    }
    return config_header;
}

pub fn addValue(config_header: *ConfigHeader, name: []const u8, comptime T: type, value: T) void {
    return addValueInner(config_header, name, T, value) catch @panic("OOM");
}

pub fn addValues(config_header: *ConfigHeader, values: anytype) void {
    inline for (@typeInfo(@TypeOf(values)).@"struct".fields) |field| {
        addValue(config_header, field.name, field.type, @field(values, field.name));
    }
}

pub fn getOutputDir(ch: *ConfigHeader) Build.LazyPath {
    return .{ .generated = .{ .file = &ch.generated_dir } };
}

pub fn getOutputFile(ch: *ConfigHeader) Build.LazyPath {
    return ch.getOutputDir().path(ch.step.owner, ch.include_path);
}

fn addValueInner(config_header: *ConfigHeader, name: []const u8, comptime T: type, value: T) !void {
    switch (@typeInfo(T)) {
        .null => {
            try config_header.values.put(name, .undef);
        },
        .void => {
            try config_header.values.put(name, .defined);
        },
        .bool => {
            try config_header.values.put(name, .{ .boolean = value });
        },
        .int => {
            try config_header.values.put(name, .{ .int = value });
        },
        .comptime_int => {
            try config_header.values.put(name, .{ .int = value });
        },
        .@"enum", .enum_literal => {
            try config_header.values.put(name, .{ .ident = @tagName(value) });
        },
        .optional => {
            if (value) |x| {
                return addValueInner(config_header, name, @TypeOf(x), x);
            } else {
                try config_header.values.put(name, .undef);
            }
        },
        .pointer => |ptr| {
            switch (@typeInfo(ptr.child)) {
                .array => |array| {
                    if (ptr.size == .one and array.child == u8) {
                        try config_header.values.put(name, .{ .string = value });
                        return;
                    }
                },
                .int => {
                    if (ptr.size == .slice and ptr.child == u8) {
                        try config_header.values.put(name, .{ .string = value });
                        return;
                    }
                },
                else => {},
            }

            @compileError("unsupported ConfigHeader value type: " ++ @typeName(T));
        },
        else => @compileError("unsupported ConfigHeader value type: " ++ @typeName(T)),
    }
}

fn make(step: *Step, options: Step.MakeOptions) !void {
    _ = options;
    const b = step.owner;
    const config_header: *ConfigHeader = @fieldParentPtr("step", step);
    if (config_header.style.getPath()) |lp| try step.singleUnchangingWatchInput(lp);

    const gpa = b.allocator;
    const arena = b.allocator;
    const io = b.graph.io;

    var man = b.graph.cache.obtain();
    defer man.deinit();

    // Random bytes to make ConfigHeader unique. Refresh this with new
    // random bytes when ConfigHeader implementation is modified in a
    // non-backwards-compatible way.
    man.hash.add(@as(u32, 0xdef08d23));
    man.hash.addBytes(config_header.include_path);
    man.hash.addOptionalBytes(config_header.include_guard_override);

    var aw: Writer.Allocating = .init(gpa);
    defer aw.deinit();
    const bw = &aw.writer;

    const header_text = "This file was generated by ConfigHeader using the Zig Build System.";
    const c_generated_line = "/* " ++ header_text ++ " */\n";
    const asm_generated_line = "; " ++ header_text ++ "\n";

    switch (config_header.style) {
        .autoconf_undef, .autoconf_at, .cmake, .meson => |file_source| {
            try bw.writeAll(c_generated_line);
            const src_path = (try file_source.getPath4(b, step)).toString(b.allocator) catch @panic("OOM");
            const contents = Io.Dir.cwd().readFileAlloc(io, src_path, arena, .limited(config_header.max_bytes)) catch |err| {
                return step.fail("unable to read '{s}' input file '{s}': {s}", .{
                    @tagName(config_header.style), src_path, @errorName(err),
                });
            };

            switch (config_header.style) {
                .autoconf_undef => try render_autoconf_undef(step, contents, bw, config_header.values, src_path),
                .autoconf_at => try render_autoconf_at(step, contents, bw, config_header.values, src_path),
                .meson => try render_meson(step, contents, bw, config_header.values, src_path),
                .cmake => try render_cmake(step, contents, bw, config_header.values, src_path),
                else => unreachable,
            }
        },
        .blank => {
            try bw.writeAll(c_generated_line);
            try render_blank(gpa, bw, config_header.values, config_header.include_path, config_header.include_guard_override);
        },
        .nasm => {
            try bw.writeAll(asm_generated_line);
            try render_nasm(bw, config_header.values);
        },
    }

    const output = aw.written();
    man.hash.addBytes(output);

    if (try step.cacheHit(&man)) {
        const digest = man.final();
        config_header.generated_dir.path = try b.cache_root.join(arena, &.{ "o", &digest });
        return;
    }

    const digest = man.final();

    // If output_path has directory parts, deal with them.  Example:
    // output_dir is zig-cache/o/HASH
    // output_path is libavutil/avconfig.h
    // We want to open directory zig-cache/o/HASH/libavutil/
    // but keep output_dir as zig-cache/o/HASH for -I include
    const sub_path = b.pathJoin(&.{ "o", &digest, config_header.include_path });
    const sub_path_dirname = std.fs.path.dirname(sub_path).?;

    b.cache_root.handle.createDirPath(io, sub_path_dirname) catch |err| {
        return step.fail("unable to make path '{f}{s}': {s}", .{
            b.cache_root, sub_path_dirname, @errorName(err),
        });
    };

    b.cache_root.handle.writeFile(io, .{ .sub_path = sub_path, .data = output }) catch |err| {
        return step.fail("unable to write file '{f}{s}': {s}", .{
            b.cache_root, sub_path, @errorName(err),
        });
    };

    config_header.generated_dir.path = try b.cache_root.join(arena, &.{ "o", &digest });
    try man.writeManifest();
}

fn render_autoconf_undef(
    step: *Step,
    contents: []const u8,
    bw: *Writer,
    values: std.StringArrayHashMap(Value),
    src_path: []const u8,
) !void {
    const build = step.owner;
    const allocator = build.allocator;

    var is_used: std.DynamicBitSetUnmanaged = try .initEmpty(allocator, values.count());
    defer is_used.deinit(allocator);

    var any_errors = false;
    var line_index: u32 = 0;
    var line_it = mem.splitScalar(u8, contents, '\n');
    while (line_it.next()) |line| : (line_index += 1) {
        if (!mem.startsWith(u8, line, "#")) {
            try bw.writeAll(line);
            try bw.writeByte('\n');
            continue;
        }
        var it = mem.tokenizeAny(u8, line[1..], " \t\r");
        const undef = it.next().?;
        if (!mem.eql(u8, undef, "undef")) {
            try bw.writeAll(line);
            try bw.writeByte('\n');
            continue;
        }
        const name = it.next().?;
        const index = values.getIndex(name) orelse {
            try step.addError("{s}:{d}: error: unspecified config header value: '{s}'", .{
                src_path, line_index + 1, name,
            });
            any_errors = true;
            continue;
        };
        is_used.set(index);
        try renderValueC(bw, name, values.values()[index]);
    }

    var unused_value_it = is_used.iterator(.{ .kind = .unset });
    while (unused_value_it.next()) |index| {
        try step.addError("{s}: error: config header value unused: '{s}'", .{ src_path, values.keys()[index] });
        any_errors = true;
    }

    if (any_errors) return error.HeaderConfigFailed;
}

fn render_autoconf_at(
    step: *Step,
    contents: []const u8,
    bw: *Writer,
    values: std.StringArrayHashMap(Value),
    src_path: []const u8,
) !void {
    const build = step.owner;
    const allocator = build.allocator;

    var is_used: std.DynamicBitSetUnmanaged = try .initEmpty(allocator, values.count());
    defer is_used.deinit(allocator);

    var any_errors = false;
    var line_index: u32 = 0;
    var line_it = mem.splitScalar(u8, contents, '\n');
    while (line_it.next()) |line| : (line_index += 1) {
        const last_line = line_it.index == line_it.buffer.len;

        const old_len = bw.buffered().len;
        expand_variables_autoconf_at(bw, line, values, &is_used) catch |err| switch (err) {
            error.MissingValue => {
                const name = bw.buffered()[old_len..];
                try step.addError("{s}:{d}: error: unspecified config header value: '{s}'", .{
                    src_path, line_index + 1, name,
                });
                any_errors = true;
                continue;
            },
            else => {
                try step.addError("{s}:{d}: unable to substitute variable: error: {s}", .{
                    src_path, line_index + 1, @errorName(err),
                });
                any_errors = true;
                continue;
            },
        };
        if (!last_line) try bw.writeByte('\n');
    }

    var unused_value_it = is_used.iterator(.{ .kind = .unset });
    while (unused_value_it.next()) |index| {
        try step.addError("{s}: error: config header value unused: '{s}'", .{ src_path, values.keys()[index] });
        any_errors = true;
    }

    if (any_errors) return error.HeaderConfigFailed;
}

fn render_meson(
    step: *Step,
    contents: []const u8,
    bw: *Writer,
    values: std.StringArrayHashMap(Value),
    src_path: []const u8,
) !void {
    const build = step.owner;
    const allocator = build.allocator;

    var is_used: std.DynamicBitSetUnmanaged = try .initEmpty(allocator, values.count());
    defer is_used.deinit(allocator);

    var any_errors = false;
    var line_index: u32 = 0;
    var line_it = mem.splitScalar(u8, contents, '\n');
    // https://mesonbuild.com/Configuration.html
    while (line_it.next()) |line| : (line_index += 1) {
        const last_line = line_it.index == line_it.buffer.len;

        const old_len = bw.buffered().len;
        expand_variables_meson(bw, line, values, &is_used) catch |err| switch (err) {
            error.MissingToken => {
                try step.addError("{s}:{d}: error: missing define name", .{ src_path, line_index + 1 });
                any_errors = true;
                continue;
            },
            error.MissingValue => {
                const name = bw.buffered()[old_len..];
                try step.addError("{s}:{d}: error: unspecified config header value: '{s}'", .{
                    src_path, line_index + 1, name,
                });
                any_errors = true;
                continue;
            },
            else => {
                try step.addError("{s}:{d}: unable to substitute variable: error: {s}", .{
                    src_path, line_index + 1, @errorName(err),
                });
                any_errors = true;
                continue;
            },
        };
        if (!last_line) try bw.writeByte('\n');
    }

    var unused_value_it = is_used.iterator(.{ .kind = .unset });
    while (unused_value_it.next()) |index| {
        try step.addError("{s}: error: config header value unused: '{s}'", .{ src_path, values.keys()[index] });
        any_errors = true;
    }

    if (any_errors) return error.HeaderConfigFailed;
}

fn render_cmake(
    step: *Step,
    contents: []const u8,
    bw: *Writer,
    values: std.StringArrayHashMap(Value),
    src_path: []const u8,
) !void {
    const build = step.owner;
    const allocator = build.allocator;

    var values_copy = try values.clone();
    defer values_copy.deinit();

    var any_errors = false;
    var line_index: u32 = 0;
    var line_it = mem.splitScalar(u8, contents, '\n');
    while (line_it.next()) |raw_line| : (line_index += 1) {
        const last_line = line_it.index == line_it.buffer.len;

        const line = expand_variables_cmake(allocator, raw_line, values) catch |err| switch (err) {
            error.InvalidCharacter => {
                try step.addError("{s}:{d}: error: invalid character in a variable name", .{
                    src_path, line_index + 1,
                });
                any_errors = true;
                continue;
            },
            else => {
                try step.addError("{s}:{d}: unable to substitute variable: error: {s}", .{
                    src_path, line_index + 1, @errorName(err),
                });
                any_errors = true;
                continue;
            },
        };
        defer allocator.free(line);

        if (!mem.startsWith(u8, line, "#")) {
            try bw.writeAll(line);
            if (!last_line) try bw.writeByte('\n');
            continue;
        }
        var it = mem.tokenizeAny(u8, line[1..], " \t\r");
        const cmakedefine = it.next().?;
        if (!mem.eql(u8, cmakedefine, "cmakedefine") and
            !mem.eql(u8, cmakedefine, "cmakedefine01"))
        {
            try bw.writeAll(line);
            if (!last_line) try bw.writeByte('\n');
            continue;
        }

        const booldefine = mem.eql(u8, cmakedefine, "cmakedefine01");

        const name = it.next() orelse {
            try step.addError("{s}:{d}: error: missing define name", .{
                src_path, line_index + 1,
            });
            any_errors = true;
            continue;
        };
        var value: Value = values_copy.get(name) orelse blk: {
            if (booldefine) {
                break :blk .{ .int = 0 };
            }
            break :blk .undef;
        };

        value = blk: {
            switch (value) {
                .boolean => |b| {
                    if (!b) {
                        break :blk .undef;
                    }
                },
                .int => |i| {
                    if (i == 0) {
                        break :blk .undef;
                    }
                },
                .string => |string| {
                    if (string.len == 0) {
                        break :blk .undef;
                    }
                },

                else => {},
            }
            break :blk value;
        };

        if (booldefine) {
            value = blk: {
                switch (value) {
                    .undef, .defined => {
                        break :blk .{ .boolean = false };
                    },
                    .boolean => |b| {
                        break :blk .{ .boolean = b };
                    },
                    .int => |i| {
                        break :blk .{ .boolean = i != 0 };
                    },
                    .string => |string| {
                        break :blk .{ .boolean = string.len != 0 };
                    },
                    else => {
                        break :blk .{ .boolean = false };
                    },
                }
            };
        } else if (value != .undef) {
            value = .{ .ident = it.rest() };
        }

        try renderValueC(bw, name, value);
    }

    if (any_errors) return error.HeaderConfigFailed;
}

fn render_blank(
    gpa: mem.Allocator,
    bw: *Writer,
    defines: std.StringArrayHashMap(Value),
    include_path: []const u8,
    include_guard_override: ?[]const u8,
) !void {
    const include_guard_name = include_guard_override orelse blk: {
        const name = try gpa.dupe(u8, include_path);
        for (name) |*byte| {
            switch (byte.*) {
                'a'...'z' => byte.* = byte.* - 'a' + 'A',
                'A'...'Z', '0'...'9' => continue,
                else => byte.* = '_',
            }
        }
        break :blk name;
    };
    defer if (include_guard_override == null) gpa.free(include_guard_name);

    try bw.print(
        \\#ifndef {[0]s}
        \\#define {[0]s}
        \\
    , .{include_guard_name});

    const values = defines.values();
    for (defines.keys(), 0..) |name, i| try renderValueC(bw, name, values[i]);

    try bw.print(
        \\#endif /* {s} */
        \\
    , .{include_guard_name});
}

fn render_nasm(bw: *Writer, defines: std.StringArrayHashMap(Value)) !void {
    for (defines.keys(), defines.values()) |name, value| try renderValueNasm(bw, name, value);
}

fn renderValueC(bw: *Writer, name: []const u8, value: Value) !void {
    switch (value) {
        .undef => try bw.print("/* #undef {s} */\n", .{name}),
        .defined => try bw.print("#define {s}\n", .{name}),
        .boolean => |b| try bw.print("#define {s} {c}\n", .{ name, @as(u8, '0') + @intFromBool(b) }),
        .int => |i| try bw.print("#define {s} {d}\n", .{ name, i }),
        .ident => |ident| try bw.print("#define {s} {s}\n", .{ name, ident }),
        // TODO: use C-specific escaping instead of zig string literals
        .string => |string| try bw.print("#define {s} \"{f}\"\n", .{ name, std.zig.fmtString(string) }),
    }
}

fn renderValueMeson(bw: *Writer, name: []const u8, value: Value) !void {
    switch (value) {
        .undef => try bw.print("/* #undef {s} */", .{name}),
        .defined => try bw.print("#define {s}", .{name}),
        .boolean => |b| {
            if (b) {
                try bw.print("#define {s}", .{name});
            } else {
                try bw.print("#undef {s}", .{name});
            }
        },
        .int => |i| try bw.print("#define {s} {d}", .{ name, i }),
        .ident => |ident| try bw.print("#define {s} {s}", .{ name, ident }),
        .string => |string| try bw.print("#define {s} \"{f}\"", .{ name, std.zig.fmtString(string) }),
    }
}

fn renderValueNasm(bw: *Writer, name: []const u8, value: Value) !void {
    switch (value) {
        .undef => try bw.print("; %undef {s}\n", .{name}),
        .defined => try bw.print("%define {s}\n", .{name}),
        .boolean => |b| try bw.print("%define {s} {c}\n", .{ name, @as(u8, '0') + @intFromBool(b) }),
        .int => |i| try bw.print("%define {s} {d}\n", .{ name, i }),
        .ident => |ident| try bw.print("%define {s} {s}\n", .{ name, ident }),
        // TODO: use nasm-specific escaping instead of zig string literals
        .string => |string| try bw.print("%define {s} \"{f}\"\n", .{ name, std.zig.fmtString(string) }),
    }
}

fn expand_variables_autoconf_at(
    bw: *Writer,
    line: []const u8,
    values: std.StringArrayHashMap(Value),
    is_used: *std.DynamicBitSetUnmanaged,
) !void {
    const valid_varname_chars = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789_";

    var curr: usize = 0;
    var source_offset: usize = 0;
    while (curr < line.len) : (curr += 1) {
        if (line[curr] != '@') continue;
        if (mem.findScalarPos(u8, line, curr + 1, '@')) |close_pos| {
            if (close_pos == curr + 1) {
                // closed immediately, preserve as a literal
                continue;
            }
            const valid_varname_end = mem.findNonePos(u8, line, curr + 1, valid_varname_chars) orelse 0;
            if (valid_varname_end != close_pos) {
                // contains invalid characters, preserve as a literal
                continue;
            }

            const key = line[curr + 1 .. close_pos];
            const index = values.getIndex(key) orelse {
                // Report the missing key to the caller.
                try bw.writeAll(key);
                return error.MissingValue;
            };
            const value = values.get(key).?;
            is_used.set(index);

            const content_before_value = line[source_offset..curr];
            switch (value) {
                .undef, .defined => try bw.print("{s}", .{content_before_value}),
                .boolean => |b| try bw.print("{s}{c}", .{ content_before_value, @as(u8, '0') + @intFromBool(b) }),
                .int => |i| try bw.print("{s}{d}", .{ content_before_value, i }),
                .ident, .string => |s| try bw.print("{s}{s}", .{ content_before_value, s }),
            }

            curr = close_pos;
            source_offset = close_pos + 1;
        }
    }

    try bw.writeAll(line[source_offset..]);
}

fn expand_variables_meson(
    bw: *Writer,
    line: []const u8,
    values: std.StringArrayHashMap(Value),
    is_used: *std.DynamicBitSetUnmanaged,
) !void {
    const mesondefine = "#mesondefine";
    if (mem.startsWith(u8, line, mesondefine)) {
        const line_offset = mesondefine.len + 1;
        if (line_offset > line.len) return error.MissingToken;
        var it = mem.tokenizeAny(u8, line[line_offset..], " \t\r");
        const name = it.next() orelse return error.MissingToken;

        const index = values.getIndex(name) orelse {
            // Report the missing key to the caller.
            try bw.writeAll(name);
            return error.MissingValue;
        };

        is_used.set(index);
        try renderValueMeson(bw, name, values.values()[index]);
        // comments/any other text passthrough unaffected
        return try bw.writeAll(line[line_offset + name.len ..]);
    }

    try expand_variables_autoconf_at(bw, line, values, is_used);
}

fn expand_variables_cmake(
    allocator: Allocator,
    contents: []const u8,
    values: std.StringArrayHashMap(Value),
) ![]const u8 {
    var result: std.array_list.Managed(u8) = .init(allocator);
    errdefer result.deinit();

    const valid_varname_chars = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789/_.+-";
    const open_var = "${";

    var curr: usize = 0;
    var source_offset: usize = 0;
    const Position = struct {
        source: usize,
        target: usize,
    };
    var var_stack: std.array_list.Managed(Position) = .init(allocator);
    defer var_stack.deinit();
    loop: while (curr < contents.len) : (curr += 1) {
        switch (contents[curr]) {
            '@' => blk: {
                if (mem.findScalarPos(u8, contents, curr + 1, '@')) |close_pos| {
                    if (close_pos == curr + 1) {
                        // closed immediately, preserve as a literal
                        break :blk;
                    }
                    const valid_varname_end = mem.findNonePos(u8, contents, curr + 1, valid_varname_chars) orelse 0;
                    if (valid_varname_end != close_pos) {
                        // contains invalid characters, preserve as a literal
                        break :blk;
                    }

                    const key = contents[curr + 1 .. close_pos];
                    const value = values.get(key) orelse return error.MissingValue;
                    const missing = contents[source_offset..curr];
                    try result.appendSlice(missing);
                    switch (value) {
                        .undef, .defined => {},
                        .boolean => |b| {
                            try result.append(if (b) '1' else '0');
                        },
                        .int => |i| {
                            try result.print("{d}", .{i});
                        },
                        .ident, .string => |s| {
                            try result.appendSlice(s);
                        },
                    }

                    curr = close_pos;
                    source_offset = close_pos + 1;

                    continue :loop;
                }
            },
            '$' => blk: {
                const next = curr + 1;
                if (next == contents.len or contents[next] != '{') {
                    // no open bracket detected, preserve as a literal
                    break :blk;
                }
                const missing = contents[source_offset..curr];
                try result.appendSlice(missing);
                try result.appendSlice(open_var);

                source_offset = curr + open_var.len;
                curr = next;
                try var_stack.append(Position{
                    .source = curr,
                    .target = result.items.len - open_var.len,
                });

                continue :loop;
            },
            '}' => blk: {
                if (var_stack.items.len == 0) {
                    // no open bracket, preserve as a literal
                    break :blk;
                }
                const open_pos = var_stack.pop().?;
                if (source_offset == open_pos.source) {
                    source_offset += open_var.len;
                }
                const missing = contents[source_offset..curr];
                try result.appendSlice(missing);

                const key_start = open_pos.target + open_var.len;
                const key = result.items[key_start..];
                if (key.len == 0) {
                    return error.MissingKey;
                }
                const value = values.get(key) orelse return error.MissingValue;
                result.shrinkRetainingCapacity(result.items.len - key.len - open_var.len);
                switch (value) {
                    .undef, .defined => {},
                    .boolean => |b| {
                        try result.append(if (b) '1' else '0');
                    },
                    .int => |i| {
                        try result.print("{d}", .{i});
                    },
                    .ident, .string => |s| {
                        try result.appendSlice(s);
                    },
                }

                source_offset = curr + 1;

                continue :loop;
            },
            '\\' => {
                // backslash is not considered a special character
                continue :loop;
            },
            else => {},
        }

        if (var_stack.items.len > 0 and mem.findScalar(u8, valid_varname_chars, contents[curr]) == null) {
            return error.InvalidCharacter;
        }
    }

    if (source_offset != contents.len) {
        const missing = contents[source_offset..];
        try result.appendSlice(missing);
    }

    return result.toOwnedSlice();
}

fn testReplaceVariablesAutoconfAt(
    allocator: Allocator,
    contents: []const u8,
    expected: []const u8,
    values: std.StringArrayHashMap(Value),
) !void {
    var aw: Writer.Allocating = .init(allocator);
    defer aw.deinit();
    const bw = &aw.writer;

    var is_used: std.DynamicBitSetUnmanaged = try .initEmpty(allocator, values.count());
    defer is_used.deinit(allocator);

    try expand_variables_autoconf_at(bw, contents, values, &is_used);

    var it = is_used.iterator(.{ .kind = .unset });
    while (it.next()) |_| return error.UnusedValue;
    try testing.expectEqualStrings(expected, aw.written());
}

fn testReplaceVariablesMeson(
    allocator: Allocator,
    contents: []const u8,
    expected: []const u8,
    values: std.StringArrayHashMap(Value),
) !void {
    var aw: Writer.Allocating = .init(allocator);
    defer aw.deinit();
    const bw = &aw.writer;

    var is_used: std.DynamicBitSetUnmanaged = try .initEmpty(allocator, values.count());
    defer is_used.deinit(allocator);

    try expand_variables_meson(bw, contents, values, &is_used);

    var it = is_used.iterator(.{ .kind = .unset });
    while (it.next()) |_| return error.UnusedValue;
    try testing.expectEqualStrings(expected, aw.written());
}

fn testReplaceVariablesCMake(
    allocator: Allocator,
    contents: []const u8,
    expected: []const u8,
    values: std.StringArrayHashMap(Value),
) !void {
    const actual = try expand_variables_cmake(allocator, contents, values);
    defer allocator.free(actual);

    try testing.expectEqualStrings(expected, actual);
}

test "expand_variables_autoconf_at simple cases" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    // empty strings are preserved
    try testReplaceVariablesAutoconfAt(allocator, "", "", values);

    // line with misc content is preserved
    try testReplaceVariablesAutoconfAt(allocator, "no substitution", "no substitution", values);

    // empty @ sigils are preserved
    try testReplaceVariablesAutoconfAt(allocator, "@", "@", values);
    try testReplaceVariablesAutoconfAt(allocator, "@@", "@@", values);
    try testReplaceVariablesAutoconfAt(allocator, "@@@", "@@@", values);
    try testReplaceVariablesAutoconfAt(allocator, "@@@@", "@@@@", values);

    // simple substitution
    try values.putNoClobber("undef", .undef);
    try testReplaceVariablesAutoconfAt(allocator, "@undef@", "", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("defined", .defined);
    try testReplaceVariablesAutoconfAt(allocator, "@defined@", "", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("true", .{ .boolean = true });
    try testReplaceVariablesAutoconfAt(allocator, "@true@", "1", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("false", .{ .boolean = false });
    try testReplaceVariablesAutoconfAt(allocator, "@false@", "0", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("int", .{ .int = 42 });
    try testReplaceVariablesAutoconfAt(allocator, "@int@", "42", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("ident", .{ .string = "value" });
    try testReplaceVariablesAutoconfAt(allocator, "@ident@", "value", values);
    values.clearRetainingCapacity();

    try values.putNoClobber("string", .{ .string = "text" });
    try testReplaceVariablesAutoconfAt(allocator, "@string@", "text", values);
    values.clearRetainingCapacity();

    // double packed substitution
    try values.putNoClobber("string", .{ .string = "text" });
    try testReplaceVariablesAutoconfAt(allocator, "@string@@string@", "texttext", values);
    values.clearRetainingCapacity();

    // triple packed substitution
    try values.putNoClobber("int", .{ .int = 42 });
    try values.putNoClobber("string", .{ .string = "text" });
    try testReplaceVariablesAutoconfAt(allocator, "@string@@int@@string@", "text42text", values);
    values.clearRetainingCapacity();

    // double separated substitution
    try values.putNoClobber("int", .{ .int = 42 });
    try testReplaceVariablesAutoconfAt(allocator, "@int@.@int@", "42.42", values);
    values.clearRetainingCapacity();

    // triple separated substitution
    try values.putNoClobber("true", .{ .boolean = true });
    try values.putNoClobber("int", .{ .int = 42 });
    try testReplaceVariablesAutoconfAt(allocator, "@int@.@true@.@int@", "42.1.42", values);
    values.clearRetainingCapacity();

    // misc prefix is preserved
    try values.putNoClobber("false", .{ .boolean = false });
    try testReplaceVariablesAutoconfAt(allocator, "false is @false@", "false is 0", values);
    values.clearRetainingCapacity();

    // misc suffix is preserved
    try values.putNoClobber("true", .{ .boolean = true });
    try testReplaceVariablesAutoconfAt(allocator, "@true@ is true", "1 is true", values);
    values.clearRetainingCapacity();

    // surrounding content is preserved
    try values.putNoClobber("int", .{ .int = 42 });
    try testReplaceVariablesAutoconfAt(allocator, "what is 6*7? @int@! /* comment */", "what is 6*7? 42! /* comment */", values);
    values.clearRetainingCapacity();

    // incomplete key is preserved
    try testReplaceVariablesAutoconfAt(allocator, "@undef", "@undef", values);

    // unknown key leads to an error
    try testing.expectError(error.MissingValue, testReplaceVariablesAutoconfAt(allocator, "@bad@", "", values));

    // unused key leads to an error
    try values.putNoClobber("int", .{ .int = 42 });
    try values.putNoClobber("false", .{ .boolean = false });
    try testing.expectError(error.UnusedValue, testReplaceVariablesAutoconfAt(allocator, "@int", "", values));
    values.clearRetainingCapacity();
}

test "expand_variables_autoconf_at edge cases" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    // @-vars resolved only when they wrap valid characters, otherwise considered literals
    try values.putNoClobber("string", .{ .string = "text" });
    try testReplaceVariablesAutoconfAt(allocator, "@@string@@", "@text@", values);
    values.clearRetainingCapacity();

    // expanded variables are considered strings after expansion
    try values.putNoClobber("string_at", .{ .string = "@string@" });
    try testReplaceVariablesAutoconfAt(allocator, "@string_at@", "@string@", values);
    values.clearRetainingCapacity();
}

test "expand_variables_meson cases" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    // missing token after mesondefine
    try testing.expectError(error.MissingToken, testReplaceVariablesMeson(allocator, "#mesondefine ", "", values));
    try testing.expectError(error.MissingToken, testReplaceVariablesMeson(allocator, "#mesondefine", "", values));

    // mesondefine token value not set (token not in values)
    try testing.expectError(error.MissingValue, testReplaceVariablesMeson(allocator, "#mesondefine unsetval", "", values));

    // mesondefine boolean true renders as bare #define (no value)
    try values.putNoClobber("trueval", .{ .boolean = true });
    try testReplaceVariablesMeson(allocator, "#mesondefine trueval", "#define trueval", values);
    try testReplaceVariablesMeson(allocator, "#mesondefine trueval /* some comments */", "#define trueval /* some comments */", values);
    values.clearRetainingCapacity();

    // mesondefine boolean false renders as bare #undef (not commented out)
    try values.putNoClobber("falseval", .{ .boolean = false });
    try testReplaceVariablesMeson(allocator, "#mesondefine falseval", "#undef falseval", values);
    try testReplaceVariablesMeson(allocator, "#mesondefine falseval /* some comments */", "#undef falseval /* some comments */", values);
    values.clearRetainingCapacity();

    // mesondefine null (explicit undef)
    try values.putNoClobber("noval", .undef);
    try testReplaceVariablesMeson(allocator, "#mesondefine noval", "/* #undef noval */", values);
    values.clearRetainingCapacity();

    // mesondefine defined (void)
    try values.putNoClobber("defval", .defined);
    try testReplaceVariablesMeson(allocator, "#mesondefine defval", "#define defval", values);
    values.clearRetainingCapacity();

    // mesondefine integer renders as #define with numeric value
    try values.putNoClobber("intval", .{ .int = 42 });
    try testReplaceVariablesMeson(allocator, "#mesondefine intval", "#define intval 42", values);
    values.clearRetainingCapacity();

    // mesondefine zero integer
    try values.putNoClobber("zeroval", .{ .int = 0 });
    try testReplaceVariablesMeson(allocator, "#mesondefine zeroval", "#define zeroval 0", values);
    values.clearRetainingCapacity();

    // mesondefine negative integer
    try values.putNoClobber("negval", .{ .int = -1 });
    try testReplaceVariablesMeson(allocator, "#mesondefine negval", "#define negval -1", values);
    values.clearRetainingCapacity();

    // mesondefine ident renders as #define with identifier value
    try values.putNoClobber("identval", .{ .ident = "raw_identifier" });
    try testReplaceVariablesMeson(allocator, "#mesondefine identval", "#define identval raw_identifier", values);
    values.clearRetainingCapacity();

    // mesondefine string renders as #define with quoted string value
    try values.putNoClobber("stringval", .{ .string = "hello" });
    try testReplaceVariablesMeson(allocator, "#mesondefine stringval", "#define stringval \"hello\"", values);
    values.clearRetainingCapacity();

    try testReplaceVariablesMeson(allocator, "@ substitution", "@ substitution", values);
}

test "expand_variables_cmake simple cases" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    try values.putNoClobber("undef", .undef);
    try values.putNoClobber("defined", .defined);
    try values.putNoClobber("true", .{ .boolean = true });
    try values.putNoClobber("false", .{ .boolean = false });
    try values.putNoClobber("int", .{ .int = 42 });
    try values.putNoClobber("ident", .{ .string = "value" });
    try values.putNoClobber("string", .{ .string = "text" });

    // empty strings are preserved
    try testReplaceVariablesCMake(allocator, "", "", values);

    // line with misc content is preserved
    try testReplaceVariablesCMake(allocator, "no substitution", "no substitution", values);

    // empty ${} wrapper leads to an error
    try testing.expectError(error.MissingKey, testReplaceVariablesCMake(allocator, "${}", "", values));

    // empty @ sigils are preserved
    try testReplaceVariablesCMake(allocator, "@", "@", values);
    try testReplaceVariablesCMake(allocator, "@@", "@@", values);
    try testReplaceVariablesCMake(allocator, "@@@", "@@@", values);
    try testReplaceVariablesCMake(allocator, "@@@@", "@@@@", values);

    // simple substitution
    try testReplaceVariablesCMake(allocator, "@undef@", "", values);
    try testReplaceVariablesCMake(allocator, "${undef}", "", values);
    try testReplaceVariablesCMake(allocator, "@defined@", "", values);
    try testReplaceVariablesCMake(allocator, "${defined}", "", values);
    try testReplaceVariablesCMake(allocator, "@true@", "1", values);
    try testReplaceVariablesCMake(allocator, "${true}", "1", values);
    try testReplaceVariablesCMake(allocator, "@false@", "0", values);
    try testReplaceVariablesCMake(allocator, "${false}", "0", values);
    try testReplaceVariablesCMake(allocator, "@int@", "42", values);
    try testReplaceVariablesCMake(allocator, "${int}", "42", values);
    try testReplaceVariablesCMake(allocator, "@ident@", "value", values);
    try testReplaceVariablesCMake(allocator, "${ident}", "value", values);
    try testReplaceVariablesCMake(allocator, "@string@", "text", values);
    try testReplaceVariablesCMake(allocator, "${string}", "text", values);

    // double packed substitution
    try testReplaceVariablesCMake(allocator, "@string@@string@", "texttext", values);
    try testReplaceVariablesCMake(allocator, "${string}${string}", "texttext", values);

    // triple packed substitution
    try testReplaceVariablesCMake(allocator, "@string@@int@@string@", "text42text", values);
    try testReplaceVariablesCMake(allocator, "@string@${int}@string@", "text42text", values);
    try testReplaceVariablesCMake(allocator, "${string}@int@${string}", "text42text", values);
    try testReplaceVariablesCMake(allocator, "${string}${int}${string}", "text42text", values);

    // double separated substitution
    try testReplaceVariablesCMake(allocator, "@int@.@int@", "42.42", values);
    try testReplaceVariablesCMake(allocator, "${int}.${int}", "42.42", values);

    // triple separated substitution
    try testReplaceVariablesCMake(allocator, "@int@.@true@.@int@", "42.1.42", values);
    try testReplaceVariablesCMake(allocator, "@int@.${true}.@int@", "42.1.42", values);
    try testReplaceVariablesCMake(allocator, "${int}.@true@.${int}", "42.1.42", values);
    try testReplaceVariablesCMake(allocator, "${int}.${true}.${int}", "42.1.42", values);

    // misc prefix is preserved
    try testReplaceVariablesCMake(allocator, "false is @false@", "false is 0", values);
    try testReplaceVariablesCMake(allocator, "false is ${false}", "false is 0", values);

    // misc suffix is preserved
    try testReplaceVariablesCMake(allocator, "@true@ is true", "1 is true", values);
    try testReplaceVariablesCMake(allocator, "${true} is true", "1 is true", values);

    // surrounding content is preserved
    try testReplaceVariablesCMake(allocator, "what is 6*7? @int@!", "what is 6*7? 42!", values);
    try testReplaceVariablesCMake(allocator, "what is 6*7? ${int}!", "what is 6*7? 42!", values);

    // incomplete key is preserved
    try testReplaceVariablesCMake(allocator, "@undef", "@undef", values);
    try testReplaceVariablesCMake(allocator, "${undef", "${undef", values);
    try testReplaceVariablesCMake(allocator, "{undef}", "{undef}", values);
    try testReplaceVariablesCMake(allocator, "undef@", "undef@", values);
    try testReplaceVariablesCMake(allocator, "undef}", "undef}", values);

    // unknown key leads to an error
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "@bad@", "", values));
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "${bad}", "", values));
}

test "expand_variables_cmake edge cases" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    // special symbols
    try values.putNoClobber("at", .{ .string = "@" });
    try values.putNoClobber("dollar", .{ .string = "$" });
    try values.putNoClobber("underscore", .{ .string = "_" });

    // basic value
    try values.putNoClobber("string", .{ .string = "text" });

    // proxy case values
    try values.putNoClobber("string_proxy", .{ .string = "string" });
    try values.putNoClobber("string_at", .{ .string = "@string@" });
    try values.putNoClobber("string_curly", .{ .string = "{string}" });
    try values.putNoClobber("string_var", .{ .string = "${string}" });

    // stack case values
    try values.putNoClobber("nest_underscore_proxy", .{ .string = "underscore" });
    try values.putNoClobber("nest_proxy", .{ .string = "nest_underscore_proxy" });

    // @-vars resolved only when they wrap valid characters, otherwise considered literals
    try testReplaceVariablesCMake(allocator, "@@string@@", "@text@", values);
    try testReplaceVariablesCMake(allocator, "@${string}@", "@text@", values);

    // @-vars are resolved inside ${}-vars
    try testReplaceVariablesCMake(allocator, "${@string_proxy@}", "text", values);

    // expanded variables are considered strings after expansion
    try testReplaceVariablesCMake(allocator, "@string_at@", "@string@", values);
    try testReplaceVariablesCMake(allocator, "${string_at}", "@string@", values);
    try testReplaceVariablesCMake(allocator, "$@string_curly@", "${string}", values);
    try testReplaceVariablesCMake(allocator, "$${string_curly}", "${string}", values);
    try testReplaceVariablesCMake(allocator, "${string_var}", "${string}", values);
    try testReplaceVariablesCMake(allocator, "@string_var@", "${string}", values);
    try testReplaceVariablesCMake(allocator, "${dollar}{${string}}", "${text}", values);
    try testReplaceVariablesCMake(allocator, "@dollar@{${string}}", "${text}", values);
    try testReplaceVariablesCMake(allocator, "@dollar@{@string@}", "${text}", values);

    // when expanded variables contain invalid characters, they prevent further expansion
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "${${string_var}}", "", values));
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "${@string_var@}", "", values));

    // nested expanded variables are expanded from the inside out
    try testReplaceVariablesCMake(allocator, "${string${underscore}proxy}", "string", values);
    try testReplaceVariablesCMake(allocator, "${string@underscore@proxy}", "string", values);

    // nested vars are only expanded when ${} is closed
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "@nest@underscore@proxy@", "", values));
    try testReplaceVariablesCMake(allocator, "${nest${underscore}proxy}", "nest_underscore_proxy", values);
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "@nest@@nest_underscore@underscore@proxy@@proxy@", "", values));
    try testReplaceVariablesCMake(allocator, "${nest${${nest_underscore${underscore}proxy}}proxy}", "nest_underscore_proxy", values);

    // invalid characters lead to an error
    try testing.expectError(error.InvalidCharacter, testReplaceVariablesCMake(allocator, "${str*ing}", "", values));
    try testing.expectError(error.InvalidCharacter, testReplaceVariablesCMake(allocator, "${str$ing}", "", values));
    try testing.expectError(error.InvalidCharacter, testReplaceVariablesCMake(allocator, "${str@ing}", "", values));
}

test "expand_variables_cmake escaped characters" {
    const allocator = testing.allocator;
    var values: std.StringArrayHashMap(Value) = .init(allocator);
    defer values.deinit();

    try values.putNoClobber("string", .{ .string = "text" });

    // backslash is an invalid character for @ lookup
    try testReplaceVariablesCMake(allocator, "\\@string\\@", "\\@string\\@", values);

    // backslash is preserved, but doesn't affect ${} variable expansion
    try testReplaceVariablesCMake(allocator, "\\${string}", "\\text", values);

    // backslash breaks ${} opening bracket identification
    try testReplaceVariablesCMake(allocator, "$\\{string}", "$\\{string}", values);

    // backslash is skipped when checking for invalid characters, yet it mangles the key
    try testing.expectError(error.MissingValue, testReplaceVariablesCMake(allocator, "${string\\}", "", values));
}
